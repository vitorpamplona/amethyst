plugins {
    alias(libs.plugins.androidApplication) apply false
    alias(libs.plugins.jetbrainsKotlinAndroid) apply false
    alias(libs.plugins.androidLibrary) apply false
    alias(libs.plugins.jetbrainsKotlinJvm) apply false
    alias(libs.plugins.androidBenchmark) apply false
    alias(libs.plugins.diffplugSpotless)
    alias(libs.plugins.googleServices) apply false
    alias(libs.plugins.jetbrainsComposeCompiler) apply false
}

// Load local properties if they exist
def localPropertiesFile = file("gradle.properties.local")
if (localPropertiesFile.exists()) {
    def localProperties = new Properties()
    localPropertiesFile.withInputStream { localProperties.load(it) }
    localProperties.each { prop ->
        project.ext.set(prop.key, prop.value)
    }
}

allprojects {
    apply plugin: 'com.diffplug.spotless'

    if (project === rootProject) {
        spotless {
            predeclareDeps()
        }
        spotlessPredeclare {
            kotlin {
                ktlint()
            }
        }
    } else {
        spotless {
            kotlin {
                target 'src/**/*.kt'

                ktlint()
                licenseHeaderFile rootProject.file('spotless/copyright.kt'), "package|import|class|object|sealed|open|interface|abstract "
            }

            groovyGradle {
                target '*.gradle'
            }
        }
    }
}

subprojects {
    afterEvaluate {
        tasks.named("preBuild") {
            dependsOn("spotlessApply")
        }
    }
}

tasks.register('installGitHook', Copy) {
    from new File(rootProject.rootDir, 'git-hooks/pre-commit')
    from new File(rootProject.rootDir, 'git-hooks/pre-push')
    into { new File(rootProject.rootDir, '.git/hooks') }
    filePermissions { unix(0777) }
}
tasks.getByPath(':amethyst:preBuild').dependsOn installGitHook
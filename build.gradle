import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        fragment_version = "1.6.1"
        lifecycle_version = '2.6.1'
        compose_ui_version = '1.5.0'
        nav_version = "2.7.1"
        room_version = "2.4.3"
        accompanist_version = '0.30.1'
        coil_version = '2.4.0'
        vico_version = '1.11.1'
        exoplayer_version = '1.1.1'
        media3_version = '1.1.1'
        core_ktx_version = '1.10.1'
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.15'
    }
}

plugins {
    id 'com.android.application' version '8.1.1' apply false
    id 'com.android.library' version '8.1.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.9.0' apply false
    id 'androidx.benchmark' version '1.1.1' apply false
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.0' apply false
}

tasks.register('installGitHook', Copy) {
    from new File(rootProject.rootDir, 'git-hooks/pre-commit')
    from new File(rootProject.rootDir, 'git-hooks/pre-push')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}
tasks.getByPath(':app:preBuild').dependsOn installGitHook

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("composeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
            if (project.findProperty("composeCompilerMetrics") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}